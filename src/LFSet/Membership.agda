{-# OPTIONS --safe #-}
module LFSet.Membership where

open import Prelude
open import Foundations.Sigma
open import Meta.Effect

open import Data.Empty hiding (_â‰ _ ; elim ; rec)
open import Data.Dec as Dec hiding (elim ; rec)
open import Data.Reflects as Reflects
open import Data.Bool as Bool hiding (elim ; rec)
open import Data.Sum
open import Data.Nat hiding (elim ; rec)
open import Data.Nat.Order.Base
open import Data.Nat.Two

open import LFSet

private variable
  â„“ â„“â€² : Level
  A : ğ’° â„“
  B : ğ’° â„“â€²

-- membership

@0 âˆˆâ‚›-LFSet : A â†’ LFSet A â†’ Prop (level-of-type A)
âˆˆâ‚›-LFSet {A} x = rec go
  where
  go : Rec A (Prop (level-of-type A))
  go .[]Ê³ = el! âŠ¥
  go .âˆ·Ê³ y z xâˆˆz = el! ((x ï¼ y) âŠâ‚ xâˆˆz .n-Type.carrier)
  go .dropÊ³ y z p =
    n-path $ ua $
      âˆ¥-âˆ¥â‚.âŠâ‚-assoc â»Â¹
    âˆ™ âˆ¥-âˆ¥â‚.âŠâ‚-ap-l âˆ¥-âˆ¥â‚.âŠâ‚-idem
    âˆ™ âˆ¥-âˆ¥â‚.âŠâ‚-trunc-l â»Â¹
  go .swapÊ³ y z u p =
    n-path $ ua $
      âˆ¥-âˆ¥â‚.âŠâ‚-assoc â»Â¹
    âˆ™ âˆ¥-âˆ¥â‚.âŠâ‚-ap-l âˆ¥-âˆ¥â‚.âŠâ‚-comm
    âˆ™ âˆ¥-âˆ¥â‚.âŠâ‚-assoc
  go .truncÊ³ = n-Type-is-of-hlevel 1

-- Agda boilerplate

private module dummy-âˆˆâ‚› where

  infix 4 _âˆˆâ‚›_

  record _âˆˆâ‚›_
    {A : ğ’° â„“} (x : A) (y : LFSet A) : ğ’° â„“ where
    constructor box
    field
      unbox : Erased ((âˆˆâ‚›-LFSet x y) .n-Type.carrier)

open dummy-âˆˆâ‚› public using (_âˆˆâ‚›_) hiding (module _âˆˆâ‚›_)

âˆˆâ‚›â‡‰ : {x : A} {xs : LFSet A} â†’ x âˆˆâ‚› xs â†’ Erased ((âˆˆâ‚›-LFSet x xs) .n-Type.carrier)
âˆˆâ‚›â‡‰ = dummy-âˆˆâ‚›._âˆˆâ‚›_.unbox

â‡‰âˆˆâ‚› : {x : A} {xs : LFSet A} â†’ Erased ((âˆˆâ‚›-LFSet x xs) .n-Type.carrier) â†’ x âˆˆâ‚› xs
â‡‰âˆˆâ‚› = dummy-âˆˆâ‚›.box

âˆˆâ‚›â‰ƒ : {x : A} {xs : LFSet A} â†’ (x âˆˆâ‚› xs) â‰ƒ Erased ((âˆˆâ‚›-LFSet x xs) .n-Type.carrier)
âˆˆâ‚›â‰ƒ =
  â‰…â†’â‰ƒ $ make-iso âˆˆâ‚›â‡‰ â‡‰âˆˆâ‚› $
  make-inverses (fun-ext Î» x â†’ refl) (fun-ext Î» x â†’ refl)

Recomputable-âˆˆâ‚› : {x : A} {xs : LFSet A} â†’ Recomputable (x âˆˆâ‚› xs)
Recomputable-âˆˆâ‚› .recompute e =
  â‡‰âˆˆâ‚› (erase ((âˆˆâ‚›â‡‰ (e .erased)) .erased))

âˆˆâ‚›-prop : {x : A} {xs : LFSet A} â†’ is-prop (x âˆˆâ‚› xs)
âˆˆâ‚›-prop {x} {xs} =
  â‰ƒâ†’is-of-hlevel 1 âˆˆâ‚›â‰ƒ $
  erased-is-of-hlevel 1 ((âˆˆâ‚›-LFSet x xs) .n-Type.carrier-is-tr)

instance
  Membership-LFSet : {A : ğ’° â„“} â†’ Membership A (LFSet A) â„“
  Membership-LFSet ._âˆˆ_ = _âˆˆâ‚›_

  H-Level-âˆˆâ‚› : âˆ€ {n} {x} {xs : LFSet A} â†’ â¦ƒ n â‰¥Ê° 1 â¦„ â†’ H-Level n (x âˆˆâ‚› xs)
  H-Level-âˆˆâ‚› {n} {xs} â¦ƒ sâ‰¤Ê°s _ â¦„ = hlevel-prop-instance (âˆˆâ‚›-prop {xs = xs})
  {-# OVERLAPPING H-Level-âˆˆâ‚› #-}

hereâ‚› : {z x : A} {xs : LFSet A} â†’ z ï¼ x â†’ z âˆˆâ‚› x âˆ· xs
hereâ‚› e = â‡‰âˆˆâ‚› $ erase âˆ£ inl e âˆ£â‚

thereâ‚› : {z x : A} {xs : LFSet A} â†’ z âˆˆâ‚› xs â†’ z âˆˆâ‚› x âˆ· xs
thereâ‚› zâˆˆ = â‡‰âˆˆâ‚› $ erase âˆ£ inr ((âˆˆâ‚›â‡‰ zâˆˆ) .erased) âˆ£â‚

{-
-- TODO useless ?
unconsâ‚› : {z x : A} {xs : LFSet A}
         {B : ğ’° â„“â€²}
       â†’ (is-prop B)
       â†’ (z ï¼ x â†’ B)
       â†’ (z âˆˆâ‚› xs â†’ B)
       â†’ z âˆˆâ‚› (x âˆ· xs)
       â†’ Erased B
unconsâ‚› {z} {x} {xs} {B} bp f g zâˆˆâˆ· =
  erase
    (âˆ¥-âˆ¥â‚.elim {A = (z ï¼ x) âŠâ‚œ (âˆˆâ‚›-LFSet z xs) .n-Type.carrier}
      {P = Î» _ â†’ B}
      (Î» _ â†’ bp)
      [ f , (Î» zâˆˆ â†’ g (â‡‰âˆˆâ‚› (erase zâˆˆ))) ]áµ¤
      (âˆˆâ‚›â‡‰ zâˆˆâˆ· .erased))
-}

âˆˆâ‚›âˆ·-â‰  : {z x : A} {xs : LFSet A} â†’ z â‰  x â†’ z âˆˆâ‚› (x âˆ· xs) â†’ z âˆˆâ‚› xs
âˆˆâ‚›âˆ·-â‰  zâ‰ x zâˆˆâˆ· =
  â‡‰âˆˆâ‚› $ erase
    (rec! [ (Î» e â†’ absurd (zâ‰ x e)) , id ]áµ¤ ((âˆˆâ‚›â‡‰ zâˆˆâˆ·) .erased))

âˆˆâ‚›âˆ·-âˆ‰á´± : {z x : A} {xs : LFSet A} â†’ z âˆˆâ‚› (x âˆ· xs) â†’ z âˆ‰ xs â†’ Erased (âˆ¥ z ï¼ x âˆ¥â‚)
âˆˆâ‚›âˆ·-âˆ‰á´± zâˆˆâˆ· zâˆ‰ =
  erase (rec! [ âˆ£_âˆ£â‚
              , (Î» zâˆˆ â†’ absurd (zâˆ‰ (â‡‰âˆˆâ‚› (erase zâˆˆ)))) ]áµ¤ ((âˆˆâ‚›â‡‰ zâˆˆâˆ·) .erased))

âˆ‰â‚›-âˆ· : {z x : A} {xs : LFSet A} â†’ z â‰  x â†’ z âˆ‰ xs â†’ z âˆ‰ (x âˆ· xs)
âˆ‰â‚›-âˆ· zâ‰ x zâˆ‰xs zâˆˆâˆ· =
  Recomputable-âŠ¥ .recompute $ erase $
  rec! [ zâ‰ x , contra (Î» q â†’ â‡‰âˆˆâ‚› $ erase q) zâˆ‰xs ]áµ¤ ((âˆˆâ‚›â‡‰ zâˆˆâˆ·) .erased)

âˆ‰â‚›-uncons : {z x : A} {xs : LFSet A} â†’ z âˆ‰ (x âˆ· xs) â†’ (z â‰  x) Ã— z âˆ‰ xs
âˆ‰â‚›-uncons zâˆ‰âˆ· = contra hereâ‚› zâˆ‰âˆ· , contra thereâ‚› zâˆ‰âˆ·

âˆ‰â‚›[] : {x : A} â†’ x âˆ‰ []
âˆ‰â‚›[] x with âˆˆâ‚›â‡‰ x
... | ()

instance
  Refl-xâˆ‰â‚›[] : {x : A} â†’ Reflects (x âˆˆâ‚› []) false
  Refl-xâˆ‰â‚›[] = ofâ¿ âˆ‰â‚›[]

âˆˆâ‚›-âˆ·â†’á´± : {z x : A} {xs : LFSet A} â†’ z âˆˆâ‚› (x âˆ· xs) â†’ Erased ((z ï¼ x) âŠâ‚ (z âˆˆâ‚› xs))
âˆˆâ‚›-âˆ·â†’á´± zâˆˆâˆ· =
  erase $
    map (map-r (Î» q â†’ â‡‰âˆˆâ‚› $ erase q)) ((âˆˆâ‚›â‡‰ zâˆˆâˆ·) .erased)

âˆˆâ‚›-âˆ·=á´± : {z : A} {s : LFSet A}
       â†’ z âˆˆâ‚› s â†’ Erased (z âˆ· s ï¼ s)
âˆˆâ‚›-âˆ·=á´± {z} {s} = elim-prop go s
  where
  go : Elim-prop Î» q â†’ z âˆˆâ‚› q â†’ Erased (z âˆ· q ï¼ q)
  go .[]Ê³ = false! â¦ƒ Refl-xâˆ‰â‚›[] â¦„ -- why
  go .âˆ·Ê³ x {xs} ih zâˆˆ =
     erase
      ((rec! [ (Î» e â†’ ap (_âˆ· x âˆ· xs) e âˆ™ drop)
             , (Î» zâˆˆâ€² â†’ swap âˆ™ ap (x âˆ·_) (ih (â‡‰âˆˆâ‚› (erase zâˆˆâ€²)) .erased))
             ]áµ¤ (âˆˆâ‚›â‡‰ zâˆˆ .erased)))
  go .truncÊ³ _ = hlevel!

âˆˆâ‚›-âˆªâˆ·â†l : {z : A} {sâ‚ sâ‚‚ : LFSet A}
        â†’ z âˆˆâ‚› sâ‚
        â†’ z âˆˆâ‚› (sâ‚ âˆªâˆ· sâ‚‚)
âˆˆâ‚›-âˆªâˆ·â†l {z} {sâ‚} {sâ‚‚} = elim-prop go sâ‚
  where
  go : Elim-prop Î» q â†’ z âˆˆâ‚› q â†’ z âˆˆâ‚› (q âˆªâˆ· sâ‚‚)
  go .[]Ê³ = false! â¦ƒ Refl-xâˆ‰â‚›[] â¦„
  go .âˆ·Ê³ x ih zâˆˆâˆ· =
    Recomputable-âˆˆâ‚› .recompute $ erase
      (rec! [ hereâ‚›  , thereâ‚› âˆ˜ ih ]áµ¤ (âˆˆâ‚›-âˆ·â†’á´± zâˆˆâˆ· .erased))
  go .truncÊ³ _ = hlevel!

âˆˆâ‚›-âˆªâˆ·â†r : {z : A} {sâ‚ sâ‚‚ : LFSet A}
        â†’ z âˆˆâ‚› sâ‚‚
        â†’ z âˆˆâ‚› (sâ‚ âˆªâˆ· sâ‚‚)
âˆˆâ‚›-âˆªâˆ·â†r {z} {sâ‚} {sâ‚‚} zâˆˆ = elim-prop go sâ‚
  where
  go : Elim-prop Î» q â†’ z âˆˆâ‚› (q âˆªâˆ· sâ‚‚)
  go .[]Ê³ = zâˆˆ
  go .âˆ·Ê³ x = thereâ‚›
  go .truncÊ³ _ = hlevel!

âˆˆâ‚›-âˆªâˆ·â†’á´± : {z : A} {sâ‚ sâ‚‚ : LFSet A}
        â†’ z âˆˆâ‚› (sâ‚ âˆªâˆ· sâ‚‚) â†’ Erased ((z âˆˆâ‚› sâ‚) âŠâ‚ (z âˆˆâ‚› sâ‚‚))
âˆˆâ‚›-âˆªâˆ·â†’á´± {z} {sâ‚} {sâ‚‚} = elim-prop go sâ‚
  where
  go : Elim-prop Î» q â†’ z âˆˆâ‚› (q âˆªâˆ· sâ‚‚) â†’ Erased ((z âˆˆâ‚› q) âŠâ‚ (z âˆˆâ‚› sâ‚‚))
  go .[]Ê³ zâˆˆsâ‚‚ = erase âˆ£ inr zâˆˆsâ‚‚ âˆ£â‚
  go .âˆ·Ê³ x {xs} ih zâˆˆâˆ· =
    erase
      (rec!
         [ (Î» e â†’ âˆ£ inl (hereâ‚› e) âˆ£â‚)
         , (Î» w â†’ map (map-l thereâ‚›) (ih (â‡‰âˆˆâ‚› (erase w)) .erased)) ]áµ¤
         (âˆˆâ‚›â‡‰ zâˆˆâˆ· .erased))
  go .truncÊ³ _ = hlevel!

âˆˆâ‚›âˆªâˆ·-âˆ‰ : {z : A} {xs ys : LFSet A} â†’ z âˆˆâ‚› (xs âˆªâˆ· ys) â†’ z âˆ‰ xs â†’ z âˆˆ ys
âˆˆâ‚›âˆªâˆ·-âˆ‰ {z} {xs} {ys} zâˆˆâˆªâˆ· zâˆ‰xs =
  â‡‰âˆˆâ‚› $ erase
    (rec! [ (Î» zâˆˆxs â†’ absurd (zâˆ‰xs zâˆˆxs))
         , (Î» zâˆˆys â†’ (âˆˆâ‚›â‡‰ zâˆˆys) .erased) ]áµ¤
         (âˆˆâ‚›-âˆªâˆ·â†’á´± zâˆˆâˆªâˆ· .erased))

âˆ‰â‚›-âˆªâˆ· : {z : A} {xs ys : LFSet A}
       â†’ z âˆ‰ (xs âˆªâˆ· ys) â†’ (z âˆ‰ xs) Ã— (z âˆ‰ ys)
âˆ‰â‚›-âˆªâˆ· {z} {xs} {ys} = elim-prop go xs
  where
  go : Elim-prop Î» q â†’ z âˆ‰ (q âˆªâˆ· ys) â†’ (z âˆ‰ q) Ã— (z âˆ‰ ys)
  go .[]Ê³ zâˆ‰ys = âˆ‰â‚›[] , zâˆ‰ys
  go .âˆ·Ê³ x {xs} ih nin =
    let (ihx , ihy) = ih (nin âˆ˜ thereâ‚›) in
    âˆ‰â‚›-âˆ· {xs = xs} (nin âˆ˜ hereâ‚›) ihx , ihy
  go .truncÊ³ zs = hlevel!

âŠ†-âˆ· : {z : A} {xs ys : LFSet A}
     â†’ xs âŠ† ys â†’ (z âˆ· xs) âŠ† (z âˆ· ys)
âŠ†-âˆ· {z} {ys} sub {x} xâˆˆ =
  â‡‰âˆˆâ‚› $ erase
    (rec! [ âˆ£_âˆ£â‚ âˆ˜ inl
          , (Î» q â†’ âˆ£ inr (âˆˆâ‚›â‡‰ (sub (â‡‰âˆˆâ‚› (erase q))) .erased) âˆ£â‚) ]áµ¤
          (âˆˆâ‚›â‡‰ xâˆˆ .erased))

opaque
  unfolding filterâ‚›
  âˆˆ-filter : {p : A â†’ Bool} {s : LFSet A} {z : A}
            â†’ âŒ p z âŒŸ â†’ z âˆˆ s
            â†’ z âˆˆ filterâ‚› p s
  âˆˆ-filter {p} {s} {z} pz = elim-prop go s
    where
    go : Elim-prop Î» q â†’ z âˆˆ q â†’ z âˆˆ filterâ‚› p q
    go .[]Ê³ = id
    go .âˆ·Ê³ x {xs} ih zâˆˆâˆ· with p x | recall p x
    ... | false | âŸª eq âŸ« =
      -- TODO need better recomputability theory for these
      â‡‰âˆˆâ‚› $ erase
        (rec! [ (Î» e â†’ false! $ (soâ‰ƒis-true $ pz) â»Â¹ âˆ™ ap p e âˆ™ eq)
              , (Î» q â†’ âˆˆâ‚›â‡‰ (ih (â‡‰âˆˆâ‚› (erase q))) .erased) ]áµ¤
              (âˆˆâ‚›â‡‰ zâˆˆâˆ· .erased))
    ... | true  | _      =
      â‡‰âˆˆâ‚› $ erase
        (rec! [ âˆ£_âˆ£â‚ âˆ˜ inl
              , (Î» q â†’ âˆ£ inr (âˆˆâ‚›â‡‰ (ih (â‡‰âˆˆâ‚› (erase q))) .erased) âˆ£â‚) ]áµ¤
              (âˆˆâ‚›â‡‰ zâˆˆâˆ· .erased))
    go .truncÊ³ _ = hlevel!

  filter-âˆˆâ‚› : {p : A â†’ Bool} {s : LFSet A} {z : A}
            â†’ z âˆˆ filterâ‚› p s
            â†’ âŒ p z âŒŸ Ã— z âˆˆ s
  filter-âˆˆâ‚› {p} {s} {z} = elim-prop go s
    where
    go : Elim-prop Î» q â†’ z âˆˆ filterâ‚› p q â†’ âŒ p z âŒŸ Ã— z âˆˆ q
    go .[]Ê³ = false! â¦ƒ Refl-xâˆ‰â‚›[] â¦„ -- why
    go .âˆ·Ê³ x {xs} ih zâˆˆf with p x | recall p x
    ... | false | _ = rec! (Î» pz zâˆˆ â†’ pz , thereâ‚› zâˆˆ) (ih zâˆˆf)
    ... | true | âŸª eq âŸ« =
      Recomputable-Ã— Recomputable-So Recomputable-âˆˆâ‚› .recompute $ erase $
          rec! [ (Î» e â†’ (soâ‰ƒis-true â»Â¹ $ ap p e âˆ™ eq) , (hereâ‚› e))
               , (Î» zâˆˆâ€² â†’ let (pz , zâˆˆ) = ih (â‡‰âˆˆâ‚› (erase zâˆˆâ€²)) in
                           pz , thereâ‚› zâˆˆ)
               ]áµ¤ (âˆˆâ‚›â‡‰ zâˆˆf .erased)
    go .truncÊ³ _ = hlevel!

  âˆ‰-filter : {p : A â†’ Bool} {s : LFSet A} {z : A}
            â†’ âŒ not (p z) âŒŸ âŠ (z âˆ‰ s)
            â†’ z âˆ‰ filterâ‚› p s
  âˆ‰-filter {s} o zâˆˆf =
     let (pz , zâˆˆ) = filter-âˆˆâ‚› {s = s} zâˆˆf in
     [ flip so-not pz , _$ zâˆˆ ]áµ¤ o

  filter-âˆ‰ : {p : A â†’ Bool} {s : LFSet A} {z : A}
            â†’ z âˆ‰ filterâ‚› p s
            â†’ âŒ not (p z) âŒŸ âŠ (z âˆ‰ s)
  filter-âˆ‰ {p} {s} {z} zâˆ‰f with p z | recall p z
  ... | true  | âŸª eq âŸ« =
    inr (contra (âˆˆ-filter (soâ‰ƒis-true â»Â¹ $ eq)) zâˆ‰f)
  ... | false | _ = inl oh

  filter-all : {p : A â†’ Bool} {s : LFSet A}
             â†’ (âˆ€ {x} â†’ x âˆˆ s â†’ âŒ p x âŒŸ)
             â†’ filterâ‚› p s ï¼ s
  filter-all {p} {s} = elim-prop go s
    where
    go : Elim-prop Î» q â†’ (âˆ€ {x} â†’ x âˆˆ q â†’ âŒ p x âŒŸ) â†’ filterâ‚› p q ï¼ q
    go .[]Ê³ _ = refl
    go .âˆ·Ê³ x {xs} ih a =
      subst (Î» q â†’ (if q then x âˆ· filterâ‚› p xs else filterâ‚› p xs) ï¼ x âˆ· xs)
            ((soâ‰ƒis-true $ a (hereâ‚› refl)) â»Â¹)
            (ap (x âˆ·_) (ih (a âˆ˜ thereâ‚›)))
    go .truncÊ³ _ = hlevel!

  filter-âŠ† : {p : A â†’ Bool} {s : LFSet A}
           â†’ filterâ‚› p s âŠ† s
  filter-âŠ† xâˆˆ = filter-âˆˆâ‚› xâˆˆ .snd

  filter-eq : âˆ€ {s} {p q : A â†’ Bool}
            â†’ (âˆ€ {x} â†’ x âˆˆ s â†’ p x ï¼ q x) â†’ filterâ‚› p s ï¼ filterâ‚› q s
  filter-eq {s} {p} {q} = elim-prop go s
    where
    go : Elim-prop Î» z â†’ (âˆ€ {x} â†’ x âˆˆ z â†’ p x ï¼ q x) â†’ filterâ‚› p z ï¼ filterâ‚› q z
    go .[]Ê³ _ = refl
    go .âˆ·Ê³ x {xs} ih a with p x | recall p x
    ... | false | âŸª eq âŸ« =
      ih (a âˆ˜ thereâ‚›) âˆ™ if-false (not-so (Â¬soâ‰ƒis-false â»Â¹ $ (a (hereâ‚› refl)) â»Â¹ âˆ™ eq)) â»Â¹
    ... | true  | âŸª eq âŸ« =
      ap (x âˆ·_) (ih (a âˆ˜ thereâ‚›)) âˆ™ if-true (soâ‰ƒis-true â»Â¹ $ (a (hereâ‚› refl)) â»Â¹ âˆ™ eq) â»Â¹
    go .truncÊ³ = hlevel!

opaque
  unfolding bindâ‚›

  bind-âˆˆá´± : {A : ğ’° â„“} {B : ğ’° â„“â€²} -- why
            {f : A â†’ LFSet B} {s : LFSet A} {z : B}
          â†’ z âˆˆ bindâ‚› f s
          â†’ Erased (âˆƒ[ x ê‰ A ] ((x âˆˆâ‚› s) Ã— (z âˆˆâ‚› f x)))
  bind-âˆˆá´± {A} {B} {f} {s} {z} = elim-prop go s
    where
    go : Elim-prop Î» q â†’ z âˆˆ bindâ‚› f q â†’ Erased (âˆƒ[ x ê‰ A ] ((x âˆˆâ‚› q) Ã— (z âˆˆâ‚› f x)))
    go .[]Ê³ = false! â¦ƒ Refl-xâˆ‰â‚›[] â¦„
    go .âˆ·Ê³ x {xs} ih xâˆˆâˆ· =
      erase (rec!
              [ (Î» zâˆˆfx â†’ âˆ£ x , hereâ‚› refl , zâˆˆfx âˆ£â‚)
              , (Î» zâˆˆfxs â†’
                    map (Î» where (q , xq , zq) â†’ q , thereâ‚› xq , zq)
                       (ih zâˆˆfxs .erased)) ]áµ¤
              (âˆˆâ‚›-âˆªâˆ·â†’á´± {sâ‚ = f x} xâˆˆâˆ· .erased))
    go .truncÊ³ x = hlevel!

  âˆˆ-bind : {A : ğ’° â„“} {B : ğ’° â„“â€²} -- why
            {f : A â†’ LFSet B} {s : LFSet A} {z : B} {y : A}
          â†’ y âˆˆ s â†’ z âˆˆ f y â†’ z âˆˆ bindâ‚› f s
  âˆˆ-bind {f} {s} {z} {y} yâˆˆ zâˆˆ = elim-prop go s yâˆˆ
    where
    go : Elim-prop Î» q â†’ y âˆˆ q â†’ z âˆˆ bindâ‚› f q
    go .[]Ê³ = false! â¦ƒ Refl-xâˆ‰â‚›[] â¦„
    go .âˆ·Ê³ x {xs} ih yâˆˆâˆ· =
      Recomputable-âˆˆâ‚› .recompute $ erase
        (rec! [ (Î» e â†’ âˆˆâ‚›-âˆªâˆ·â†l {sâ‚ = f x} (subst (Î» q â†’ z âˆˆâ‚› f q) e zâˆˆ))
              , âˆˆâ‚›-âˆªâˆ·â†r {sâ‚ = f x} âˆ˜ ih
              ]áµ¤
           (âˆˆâ‚›-âˆ·â†’á´± {x = x} yâˆˆâˆ· .erased))
    go .truncÊ³ x = hlevel!
